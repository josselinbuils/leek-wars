include('map');
include('store');
include('utils');

global FIGHT_EFFECTS = @[EFFECT_DAMAGE, EFFECT_POISON];
global MARK_ENEMY_CELLS_TO_FIRE = false;

function canFireOnCellFrom(@casterCell, @targetCell, @arm) {
	if (targetCell === null || casterCell === null) {
		return false;
	}

	var isArmChip = isChip(@arm);
	var isInline = isArmChip ?
		@(isInlineChip(@arm)) :
		@(isInlineWeapon(@arm));

	if (isInline && !isOnSameLine(@casterCell, @targetCell)) {
		return false;
	}

	var distance = @(getCellDistance(@casterCell, @targetCell));
	var maxRange = isArmChip ?
		@(getChipMaxRange(@arm)) :
		@(getWeaponMaxRange(@arm));
	var minRange = isArmChip ?
		@(getChipMinRange(@arm)) :
		@(getWeaponMinRange(@arm));

	if (distance < minRange || distance > maxRange) {
		return false;
	}

	return @(lineOfSight(@casterCell, @targetCell));
}

function ensureWearingWeapon(@weapon) {
	if (getWeapon() !== weapon) {
		setWeapon(@weapon);
	}
}

function getAliveAlliesCells() {
	var cacheKey = 'tmp-alv-ally-cells';

	if (hasInStore(cacheKey)) {
		return @(getInStore(cacheKey));
	}

	var alliesCells = @(arrayMap(@(getAliveAllies()), function(@ally) {
		return getCell(ally);
	}));
	putInStore(cacheKey, @alliesCells);
	return @alliesCells;
}

function getAliveEnemiesCells() {
	var cacheKey = 'tmp-alv-enmy-cells';

	if (hasInStore(cacheKey)) {
		return @(getInStore(cacheKey));
	}

	var enemyCells = @(arrayMap(@(getAliveEnemies()), function(@enemy) {
		return getCell(enemy);
	}));
	putInStore(cacheKey, @enemyCells);
	return @enemyCells;
}

function getAreaImpactedCells(@targetCell, @area) {
	var x = @(getCellX(@targetCell));
	var y = @(getCellY(@targetCell));
	var deltas = area === AREA_CIRCLE_1 ?
		@[
			@[0, 1],
			@[1, 0],
			@[0, -1],
			@[-1, 0]
		] :
		area === AREA_CIRCLE_2 ? [
			@[0, -2],
			@[0, -1],
			@[0, 1],
			@[0, 2],
			@[-2, 0],
			@[-1, 0],
			@[1, 0],
			@[2, 0],
			@[-1, -1],
			@[-1, 1],
			@[1, -1],
			@[1, 1],
		] :
		area === AREA_CIRCLE_3 ?
		@[
			@[0, -3],
			@[0, -2],
			@[0, -1],
			@[0, 1],
			@[0, 2],
			@[0, 3],
			@[-3, 0],
			@[-2, 0],
			@[-1, 0],
			@[1, 0],
			@[2, 0],
			@[3, 0],
			@[-2, -1],
			@[-2, 1],
			@[-1, -2],
			@[-1, -1],
			@[-1, 1],
			@[-1, 2],
			@[1, -2],
			@[1, -1],
			@[1, 1],
			@[1, 2],
			@[2, -1],
			@[2, 1],
		] :
		null;

	if (deltas === null) {
		debugE('Invalid area: ' + @area);
		return @[];
	}

	var deltaCells = @(arrayMap(@deltas, function(@delta) {
		return @(getCellFromXY(x + delta[0], y + delta[1]));
	}));

	return @(ensureArray(
		@(arrayFilter(@deltaCells, function(@cell) {
			return cell !== null;
		}))
	));
}

function getArmName(@arm) {
	return isChip(arm) ?
		getChipName(@arm) :
		getWeaponName(@arm);
}

function getCellsToAreaFireOnCell(@arm, @caster, @targetCell) {
	var cellsToFire = @[];
	var targetableDeltaCells = @(getTargetableDeltaCells(@targetCell, @arm));
	var isArmChip = @(isChip(@arm));

	for (var cell in @targetableDeltaCells) {
		// Do not ignore allies as an ally could be on fire line otherwise
		var newCellsToFire = isArmChip ?
			@(getCellsToUseChipOnCell(
				@arm, @cell,
			)) :
			@(getCellsToUseWeaponOnCell(
				@arm, @cell,
			));

		for (var newCellToFire in @newCellsToFire) {
			if (!inArray(@cellsToFire, @newCellToFire)) {
				push(cellsToFire, @newCellToFire);
			}
		}
	}

	return @cellsToFire;
}

function getCellsToDirectFireOnCell(@arm, @caster, @targetCell) {
	var casterCell = @(getCell(@caster));
	var ignoredCells = @[];
	var isArmChip = @(isChip(@arm));
	var area = isArmChip ?
		getChipArea(@arm) :
		getWeaponArea(@arm);

	// The getCellsToUseWeapon function does not take into acount that
	// the laser weapons can cross enemies
	if (area === AREA_LASER_LINE) {
		pushAll(
			ignoredCells,
			isAlly(@caster) ?
			@(getAliveEnemiesCells()) :
			@(getAliveAlliesCells())
		);
	}

	// Do not ignore allies as an ally could be on fire line otherwise
	var cells = isArmChip ?
		@(getCellsToUseChipOnCell(
			@arm, @targetCell, @ignoredCells
		)) :
		@(getCellsToUseWeaponOnCell(
			@arm, @targetCell, @ignoredCells
		));

	return @(ensureArray(
		@(arrayFilter(
			@cells,
			function(@cell) {
				return cell === casterCell || !inArray(@ignoredCells, @cell);
			}
		))
	));
}

function getCellToAreaFireOnCell(@arm, @caster, @targetCell) {
	var casterCell = @(getCell(@caster));
	var cellToFire = null;
	var cacheKey = 'tmp-areaFireCell-' + arm + '-' + targetCell + '-' + casterCell;

	if (hasInStore(cacheKey)) {
		cellToFire = @(getInStore(@cacheKey));
	} else {
		var cellsToFire = @(getCellsToAreaFireOnCell(
			@arm, @caster, @targetCell
		));

		if (safeCount(@cellsToFire) > 0) {
			cellsToFire = @(getMinPathCells(@cellsToFire, @casterCell));

			if (safeCount(@cellsToFire) > 0) {
				cellToFire = getFarthestCellFrom(@cellsToFire, @targetCell);
			}
		}
		putInStore(@cacheKey, @cellToFire);
	}

	return @cellToFire;
}

function getCellToAreaFireOn(@arm, @fireCell, @targetCell) {
	var cacheKey = 'tmp-cellAreaFireOn-' + arm + '-' + targetCell;

	if (hasInStore(@cacheKey)) {
		return @(getInStore(@cacheKey));
	}

	var targetableDeltaCells = @(getTargetableDeltaCells(
		@targetCell, @arm
	));

	if (safeCount(@targetableDeltaCells) > 0) {
		for (var cell in @targetableDeltaCells) {
			if (canFireOnCellFrom(@fireCell, @cell, @arm)) {
				putInStore(@cacheKey, @cell);
				return @cell;
			}
		}
	}

	return null;
}

function getCellToDirectFireOnCell(@arm, @caster, @targetCell) {
	var casterCell = @(getCell(@caster));
	var cellToFire = null;
	var cacheKey = 'tmp-directFireCell-' + arm + '-' + targetCell + '-' + casterCell;

	if (hasInStore(cacheKey)) {
		cellToFire = @(getInStore(@cacheKey));
	} else {
		if (canFireOnCellFrom(@casterCell, @targetCell, @arm)) {
			cellToFire = @casterCell;
		} else {
			var cellsToFire = getCellsToDirectFireOnCell(
				@arm, @caster, @targetCell
			);

			if (safeCount(@cellsToFire) > 0) {
				//if (@caster !== getLeek()) {
				//logDetail('getCellsToDirectFireOnCell: ' + cellsToFire);
				//}

				cellsToFire = @(getMinPathCells(@cellsToFire, @casterCell));

				//if (@caster !== getLeek()) {
				//logDetail('getMinPathCells: ' + cellsToFire);
				//}

				if (safeCount(@cellsToFire) > 0) {
					cellToFire = @(getFarthestCellFrom(@cellsToFire, @targetCell));
				}
			}
		}
		putInStore(@cacheKey, @cellToFire);
	}

	//if (@caster !== getLeek()) {
	//logDetail('cellToFire: ' + cellToFire);
	//}

	return @cellToFire;
}

function getDangerousEnemies() {
	return @(ensureArray(
		@(arrayFilter(@(getAliveEnemies()), function(@enemy) {
			var arms = arrayConcat(
				@(getEnemyFightChips(@enemy)),
				@(ensureArray(@(getWeapons(@enemy))))
			);
			return safeCount(arms) > 0;
		}))
	));
}

function getEnemyFightChips(@enemy) {
	return @(ensureArray(
		@(arrayFilter(@(getChips(enemy)), function(@chip) {
			for (var effect in @(getChipEffects(chip))) {
				if (inArray(@FIGHT_EFFECTS, effect[0])) {
					return getCooldown(chip, enemy) <= 1;
				}
			}
			return false;
		}))
	));
}

function getEnemySortedArms(@enemy) {
	var cacheKey = 'tmp-sortedArms-' + enemy;

	if (hasInStore(@cacheKey)) {
		return @(getInStore(@cacheKey));
	}

	var arms = @(arrayConcat(
		@(getEnemyFightChips(@enemy)),
		@(ensureArray(@(getWeapons(@enemy))))
	));
	var sortedArms = @(arraySort(@arms, function(@a, @b) {
		var dammagesA = @(getArmDammages(@a, @enemy));
		var dammagesB = @(getArmDammages(@b, @enemy));
		return dammagesA > dammagesB ?
			-1 :
			dammagesA === dammagesB ? 0 : 1;
	}));

	putInStore(@cacheKey, @sortedArms);
	return @sortedArms;
}

function getEnemyPotentialDammagesOnCell(@cell, @enemy) {
	var arms = @(getEnemySortedArms(@enemy));
	var enemyMP = @(getTotalMP(@enemy));
	var enemyTP = @(getTotalTP(@enemy));
	var enemyCell = @(getCell(@enemy));
	var potentialDammages = 0;

	for (var arm in @arms) {
		var isArmChip = @(isChip(@arm));
		var cellToFire = @(getCellToDirectFireOnCell(@arm, @enemy, @cell));
		var pathLength = @(getPathLength(@enemyCell, @cellToFire));
		var armCost = isArmChip ?
			@(getChipCost(@arm)) :
			@(getWeaponCost(@arm));
		var initialArmCost = armCost + (
			!isArmChip && getWeapon(@enemy) !== @arm ? 1 : 0
		);

		//logDetail(getArmName(@arm) + ': ' + pathLength + ' / ' + enemyMP);

		if (cellToFire !== null && enemyTP > initialArmCost && enemyMP >= pathLength) {
			var armDammages = @(getArmDammages(@arm, @enemy));
			var cooldown = isArmChip ? @(getChipCooldown(@arm)) : 0;

			enemyMP -= pathLength;
			enemyTP -= initialArmCost;
			potentialDammages += armDammages;

			if (cooldown === 0) {
				while (enemyTP >= armCost) {
					enemyTP -= armCost;
					potentialDammages += armDammages;
				}
			}

			if (enemyTP <= 1) {
				break;
			}
		}
	}

	logDetail(@cell + ': ' + potentialDammages);

	return @potentialDammages;
}

function getArmDammages(@arm, @caster) {
	var cacheKey = 'tmp-armDammages-' + arm + '-' + caster;

	if (!hasInStore(cacheKey)) {
		var effects = isChip(@arm) ?
			getChipEffects(@arm) :
			getWeaponEffects(@arm);
		var dammages = 0;

		for (var effect in @effects) {
			if (effect[0] === EFFECT_DAMAGE) {
				dammages += effect[1] * max(effect[3], 1) * (1 + getStrength(@caster) / 100);
			} else if (effect[0] === EFFECT_POISON) {
				dammages += effect[1] * max(effect[3], 1) * (1 + getMagic(@caster) / 100);
			}
		}

		putInStore(@cacheKey, @dammages);
	}
	return @(getInStore(@cacheKey));
}

function getLeekName(@leek) {
	if (leek === null) {
		return null;
	}
	return getName(@leek) +
		(isSummon(@leek) ? ('_' + leek) : '');
}

function getArmMaxRange(@arm) {
	var maxRange = isChip(@arm) ?
		getChipMaxRange(@arm) :
		getWeaponMaxRange(@arm);
	var area = isChip(@arm) ?
		getChipArea(@arm) :
		getWeaponArea(@arm);

	maxRange += area === AREA_CIRCLE_1 ?
		1 :
		area === AREA_CIRCLE_2 ?
		2 :
		area === AREA_CIRCLE_3 ?
		3 :
		0;

	return maxRange;
}

function getMaxRangeArm(@arms) {
	if (safeCount(@arms) === 0) {
		return null;
	}

	var maxRangeArm = null;
	var maxRange = 0;

	for (var arm in @arms) {
		var armMaxRange = getArmMaxRange(arm);

		if (
			armMaxRange > maxRange ||
			(armMaxRange === maxRange && isAreaArm)
		) {
			maxRangeArm = arm;
			maxRange = armMaxRange;
		}
	}
	return maxRangeArm;
}

function getNearestLeekEnemy() {
	var startCell = @(getCell());
	var enemies = @(arrayFilter(
		@(getAliveEnemies()),
		function(@enemy) {
			return !isSummon(@enemy);
		}
	));
	var nearestEnemy = shift(enemies);
	var nearestEnemyPathLength = @(getPathLength(@startCell, @(getCell(@nearestEnemy))));

	for (var enemy in enemies) {
		var enemyPathLength = @(getPathLength(@startCell, @(getCell(@enemy))));

		if (
			enemyPathLength !== null &&
			enemyPathLength < nearestEnemyPathLength
		) {
			nearestEnemy = @enemy;
			nearestEnemyPathLength = @enemyPathLength;
		}
	}
	return @nearestEnemy;
}

function getNearestDangerousSummonEnemy() {
	var startCell = @(getCell());
	var enemies = @(arrayFilter(
		@(getDangerousEnemies()),
		function(@enemy) {
			return isSummon(@enemy);
		}
	));
	var nearestEnemy = null;
	var nearestEnemyPathLength = 999;

	for (var enemy in enemies) {
		var enemyPathLength = @(getPathLength(@startCell, @(getCell(@enemy))));

		if (
			enemyPathLength !== null &&
			enemyPathLength < nearestEnemyPathLength
		) {
			nearestEnemy = @enemy;
			nearestEnemyPathLength = @enemyPathLength;
		}
	}
	return @nearestEnemy;
}

function getTargetableDeltaCells(@targetCell, @arm) {
	var targetableCells = @[];
	var x = @(getCellX(@targetCell));
	var y = @(getCellY(@targetCell));
	var isChip = isChip(@arm);

	var area = isChip ?
		@(getChipArea(@arm)) :
		@(getWeaponArea(@arm));

	var range = area === AREA_CIRCLE_1 ? 1 : (
		area === AREA_CIRCLE_2 ? 2 : (
			area === AREA_CIRCLE_3 ? 3 : 0
		)
	);

	if (range === 0) {
		logError(
			'getTargetableDeltaCells',
			'invalid arm area: ' + @(getArmName(@arm)) +
			' ' + @area
		);
		return @[];
	}

	for (var i = (x - range); i <= (x + range); i++) {
		for (var j = (y - range); j <= (y + range); j++) {
			var cell = @(getCellFromXY(@i, @j));

			if (
				cell !== null &&
				!isObstacle(@cell) &&
				inArray(
					@(getAreaImpactedCells(@cell, @area)), @targetCell
				)
			) {
				push(targetableCells, @cell);
			}
		}
	}

	return arraySort(@targetableCells, function(a, b) {
		var distA = getDistance(@a, @targetCell);
		var distB = getDistance(@b, @targetCell);
		return distA < distB ? -1 : (distA === distB ? 0 : 1);
	});
}

function isAreaArm(@arm) {
	var area = isChip(@arm) ?
		@(getChipArea(@arm)) :
		@(getWeaponArea(@arm));

	return area === AREA_CIRCLE_1 ||
		area === AREA_CIRCLE_2 ||
		area === AREA_CIRCLE_3;
}

function isBadlyPoisoned() {
	var poisonDammages = 0;

	for (var effect in getEffects()) {
		if (effect[0] === EFFECT_POISON) {
			poisonDammages += effect[1];
		}
	}
	return poisonDammages >= 300;
}

function isPoisoned(@leek) {
	for (var effect in @(getEffects(leek))) {
		if (effect[0] === EFFECT_POISON) {
			return true;
		}
	}
	return false;
}

function isPoisoner(@leek) {
	var cacheKey = 'psnr-' + leek;

	if (hasInStore(@cacheKey)) {
		return @(getInStore(@cacheKey));
	}

	var isLeekPoisoner = getMagic(@leek) > getStrength(@leek);
	putInStore(@cacheKey, @isLeekPoisoner);
	return @isLeekPoisoner;
}

function summonBulb(@bulb, @ai, @enemy) {
	var enemyCell = getCell(enemy);
	var summonerCell = getCell();
	var x = getCellX(summonerCell);
	var y = getCellY(summonerCell);
	var deltas;

	if (bulb === CHIP_HEALER_BULB || bulb === CHIP_ICED_BULB) {
		deltas = @[
			[0, -2],
			[0, 2],
			[-2, 0],
			[2, 0],
		];
	} else if (
		bulb === CHIP_FIRE_BULB ||
		bulb === CHIP_PUNY_BULB ||
		bulb === CHIP_ROCKY_BULB
	) {
		deltas = @[
			[0, -3],
			[0, -2],
			[0, 2],
			[0, 3],
			[-3, 0],
			[-2, 0],
			[2, 0],
			[3, 0],
			[-2, -1],
			[-2, 1],
			[-1, -2],
			[-1, 2],
			[1, -2],
			[1, 2],
			[2, -1],
			[2, 1],
		];
	}

	var deltaCells = @(arrayMap(@deltas, function(@delta) {
		return getCellFromXY(x + delta[0], y + delta[1]);
	}));

	var possibleCells = @(arrayFilter(@deltaCells, function(@cell) {
		return cell !== null && canUseChipOnCell(bulb, cell);
	}));

	if (safeCount(@possibleCells) > 0) {
		var targetCell = bulb === CHIP_HEALER_BULB ?
			getFarthestCellFrom(@possibleCells, enemyCell) :
			getClosestCellTo(@possibleCells, enemyCell);

		showResult(summon(bulb, targetCell, ai));
	} else {
		logWarning('summonBulb', 'no cell available for bulb');
	}
}
