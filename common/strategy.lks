include('fight');
include('logger');
include('map');
include('store');
include('utils');

// TODO prioritize ref arms by dammages
// Move anyway if I can kill him
// TODO take into accout distance from healer bulb in cell safety computing
// TODO priorize most efficient cells when area fire

global LOG_MONITOR_DATA = false;
global MARK_SAFE_CELLS_TO_MOVE_ON = true;
global MAX_OPS_PERCENT = 30;
global MIN_ACCEPTABLE_SAFE_LEVEL = 80;
global MIN_DIRECT_CELLS_AROUND = 2;
global SAFE_LEVEL = 80;

global turnsWithoutFire = 0;

function canFire(@arm, @target) {
	var doLog = !isSummon();
	var debugOperations;

	if (doLog) {
		logInfo('canFire(' + getArmName(@arm) + ')');
		increaseLogIndent();
		debugOperations = monitorOperations('canFire', true);
	}

	var isArmChip = @(isChip(@arm));
	var cost = isArmChip ?
		@(getChipCost(@arm)) :
		@(getChipCost(@arm));

	if (!isArmChip && getWeapon() !== @arm) {
		cost = cost + 1;
	}

	var can = getTP() >= cost;

	if (can && isArmChip) {
		can = getCooldown(@arm) === 0;
	}

	if (
		can &&
		target !== getLeek() &&
		getCellToFire(@arm, @target) === null
	) {
		var cellToDirectFire = @(getCellToDirectFire(@arm, @target));

		if (canFireFromCell(@arm, @target, @cellToDirectFire)) {
			setCellToFire(@arm, @target, @cellToDirectFire);
		} else if (isAreaArm(@arm)) {
			var cellToAreaFire = @(getCellToAreaFire(@arm, @target));

			if (
				cellToAreaFire !== cellToDirectFire &&
				canFireFromCell(@arm, @target, @cellToAreaFire)
			) {
				setCellToFire(@arm, @target, @cellToAreaFire);
			} else {
				can = false;
			}
		} else {
			can = false;
		}
	}

	if (doLog) {
		decreaseLogIndent();
		logDetail(can);
		debugOperations();
	}
	return @can;
}

function canFireFromCell(@arm, @target, @cellToFire) {
	if (cellToFire === null) {
		return false;
	}

	var pathlength = @(getPathLength(@(getCell()), @cellToFire));
	var can = pathlength !== null && getMP() >= pathlength;

	if (can && !isSummon()) {
		can = shouldFire(@target, @arm, @cellToFire, @pathlength) === true;
	}
	return @can;
}

function fire(@arm, @target) {
	logInfo('fire(' + getArmName(@arm) + ', ' + getLeekName(@target) + ')');

	var hasFired = false;
	var hasError = false;
	var result = null;

	if (isWeapon(@arm)) {
		// Has to be done before calling getTargetEnemy
		moveTowardCell(@(getCellToFire(@arm, @target)));
		increaseLogIndent();
		var newTarget = @(getTargetEnemy(@arm, @target));
		decreaseLogIndent();

		if (newTarget !== null) {
			ensureWearingWeapon(@arm);
			result = @(useWeapon(@newTarget));
			hasFired = true;
		} else if (isAreaArm(@arm)) {
			var targetCell = getCellToAreaFireOn(@arm, @target);

			if (targetCell !== null) {
				logDetail('fire on delta cell: ' + targetCell);
				ensureWearingWeapon(@arm);
				result = @(useWeaponOnCell(@targetCell));
				hasFired = true;
			} else {
				logError('fire', 'Cannot use weapon ' + getWeaponName(@arm) + ' on cell ' + targetCell);
			}
		} else {
			logWarning('fire', 'unable to find a target enemy');
		}
	} else {
		if (target !== getLeek()) {
			moveTowardCell(@(getCellToFire(@arm, @target)));

			if (!isAlly(target)) {
				hasFired = true;
			}
		}

		if (canUseChip(arm, target)) {
			result = useChip(arm, target);
		} else if (isAreaArm(arm)) {
			var targetCell = getCellToAreaFireOn(@arm, @target);

			if (targetCell !== null) {
				logDetail('fire on delta cell: ' + targetCell);
				result = @(useChipOnCell(@arm, @targetCell));
				hasFired = true;
			} else {
				logError('fire', 'Cannot use chip ' + getChipName(arm) + ' on cell ' + targetCell);
			}
		} else {
			logError('fire', 'Cannot use chip ' + getChipName(arm) + ' on target ' + getLeekName(@target));
		}
	}

	showResult(result);

	if (
		result !== USE_SUCCESS &&
		result !== USE_FAILED &&
		result !== USE_CRITICAL
	) {
		hasError = true;
	}

	return @[hasFired, hasError];
}

function getCellSafetyLevel(@cell) {
	if (cell === null) {
		logError('getCellSafetyLevel', 'called with null cell');
		return 0;
	}

	var cacheKey = 'tmp-sfy-' + @cell;

	if (hasInStore(@cacheKey)) {
		return @(getInStore(@cacheKey));
	}

	var debugOperations = @(monitorOperations('getCellSafetyLevel', false));
	var level = 0;
	var highlyProtectedAgainstLeeks = true;
	var protectedAgainstBulbs = true;
	var protectedAgainstLeeks = true;
	var middlyProtectedAgainstBulbs = true;
	var middlyProtectedAgainstLeeks = true;
	var littleProtectedAgainstLeeks = true;

	var nearestLeekEnemy = getNearestLeekEnemy();
	var nearestLeekEnemyPathLength = @(getEnemyPathLengthToFireOnCell(
		@cell, @nearestLeekEnemy, false
	));

	if (nearestLeekEnemyPathLength !== null) {
		var nearestLeekEnemyMP = @(getTotalMP(@nearestLeekEnemy));

		highlyProtectedAgainstLeeks = false;

		if (nearestLeekEnemyPathLength <= (nearestLeekEnemyMP * 1 / 3)) {
			protectedAgainstLeeks = false;
			middlyProtectedAgainstLeeks = false;
			littleProtectedAgainstLeeks = false;
		} else if (nearestLeekEnemyPathLength <= (nearestLeekEnemyMP * 2 / 3)) {
			protectedAgainstLeeks = false;
			middlyProtectedAgainstLeeks = false;
		} else if (nearestLeekEnemyPathLength <= nearestLeekEnemyMP) {
			protectedAgainstLeeks = false;
		}
	}

	var nearestBulbEnemy = getNearestDangerousSummonEnemy();

	if (nearestBulbEnemy !== null) {
		var nearestBulbEnemyPathLength = @(getEnemyPathLengthToFireOnCell(
			@cell, @nearestBulbEnemy, false
		));

		if (nearestBulbEnemyPathLength !== null) {
			var nearestBulbEnemyMP = @(getTotalMP(@nearestBulbEnemy));

			if (nearestBulbEnemyPathLength <= (nearestBulbEnemyMP * 2 / 3)) {
				protectedAgainstBulbs = false;
				middlyProtectedAgainstBulbs = false;
			} else if (nearestBulbEnemyPathLength <= nearestBulbEnemyMP) {
				protectedAgainstBulbs = false;
			}
		}
	}

	if (
		protectedAgainstBulbs &&
		highlyProtectedAgainstLeeks
	) {
		level = 120;
	} else if (
		middlyProtectedAgainstBulbs &&
		highlyProtectedAgainstLeeks
	) {
		level = 110;
	} else if (
		protectedAgainstBulbs &&
		protectedAgainstLeeks
	) {
		level = 100;
	} else if (
		middlyProtectedAgainstBulbs &&
		protectedAgainstLeeks
	) {
		level = 80;
	} else if (protectedAgainstLeeks) {
		level = 70;
	} else if (middlyProtectedAgainstLeeks) {
		level = 60;
	} else if (littleProtectedAgainstLeeks) {
		level = 40;
	}

	if (!lineOfSight(
			@(getCell(@nearestLeekEnemy)),
			@cell
		)) {
		level += 10;
	}

	if (isBorderCell(@cell)) {
		level -= 10;
	}

	putInStore(@cacheKey, @level);
	debugOperations();
	return @level;
}

function getCellToFire(@arm, @target) {
	return @(getInStore('tmp-fire-cell-' + arm + '-' + target));
}

function computeSafetyMap(@enemy) {
	var debugOperations = @(monitorOperations('computeSafetyMap', true));
	var map = @[];
	var groups = @[];
	var refArms = @(getEnemyRefArms(@enemy));
	var refChip = @(refArms['chip']);
	var refWeapon = @(refArms['weapon']);
	var maxRange = @(max(
		refChip !== null ? @(getArmMaxRange(@refChip)) : 0,
		refWeapon !== null ? @(getArmMaxRange(@refWeapon)) : 0,
	));
	var totalEnemyMP = getTotalMP(enemy);
	var maxEnemyRange = maxRange + totalEnemyMP;
	var enemyCell = @(getCell(@enemy));
	var reachableCells = @(getReachableCells(@(getCell()), @(getMP())));

	for (var cell in reachableCells) {
		if (getDistance(enemyCell, cell) > maxEnemyRange) {
			map[@cell] = 120;
		} else {
			map[@cell] = @(getCellSafetyLevel(@cell));
		}

		var level = @(map[@cell]);

		if (groups[@level] === null) {
			groups[@level] = @[@cell];
		} else {
			push(groups[@level], @cell);
		}
	}
	debugOperations();

	arrayIter(@groups, function(@level, @group) {
		var rgb = hslToRgb(level * 1.2 / 360, 1, 0.5);
		debug(level + ': ' + rgb);
		var color = getColor(@(rgb[0]), @(rgb[1]), @(rgb[2]));
		mark(@group, @color);
	});
}

function getNextCell(@enemy, @startCell, @availableMP, @shouldMark) {
	var nextCell = @startCell;
	var enemyCell = @(getCell(@enemy));
	var cacheKey = 'tmp-nxtCl-' + @enemy + '-' +
		@startCell + '-' + @availableMP;

	logInfo('getNextCell()');
	logDetail('enemy: ' + @(getLeekName(@enemy)));
	logDetail('startCell: ' + @startCell);
	logDetail('availableMP: ' + @availableMP);

	if (hasInStore(@cacheKey)) {
		logDetail('use cache');
		nextCell = @(getInStore(@cacheKey));

		if (
			MARK_SAFE_CELLS_TO_MOVE_ON &&
			shouldMark &&
			getCellSafetyLevel(@nextCell) >= SAFE_LEVEL
		) {
			mark(@[@nextCell], getColor(255, 128, 210));
		}
	} else if (availableMP > 0) {
		var debugOperations = @(monitorOperations('getNextCell', true));

		var reachableCells = @(sortByDistanceAsc(
			@(getReachableCells(@startCell, @availableMP)),
			@enemyCell
		));
		logDetail(safeCount(reachableCells) + ' reachable cells found');

		var significantEnemyMP = getTotalMP(@enemy) * 1 / 3;
		var nonEasilyReachableCells = @(arrayFilter(@reachableCells, function(cell) {
			var pathLength = getPathLength(@enemyCell, @cell);
			return pathLength === null ||
				pathLength > significantEnemyMP;
		}));

		var debugOperationsInt = @(monitorIntermediateOperations(
			'iterates cells safety (' + @(safeCount(@nonEasilyReachableCells)) +
			' entries)'
		));
		var maxSafetyLevel = 0;
		var maxSafetyCells = @[];
		var shouldContinue = perfLimiter(MAX_OPS_PERCENT);
		var i = 0;

		for (var cell in @nonEasilyReachableCells) {
			if (!shouldContinue()) {
				logDetail('stopped by limiter after ' + @i + ' iterations');
				break;
			}

			i++;
			var safetyLevel = @(getCellSafetyLevel(@cell));

			if (safetyLevel > maxSafetyLevel) {
				maxSafetyLevel = safetyLevel;
				maxSafetyCells = @[@cell];
			} else if (safetyLevel === maxSafetyLevel) {
				push(maxSafetyCells, @cell);
			}
		}
		debugOperationsInt();

		logDetail(@safeCount(@maxSafetyCells) + ' cells found with safety ' + @maxSafetyLevel);

		if (maxSafetyLevel >= SAFE_LEVEL) {
			nextCell = @(getClosestCellTo(
				@maxSafetyCells, @enemyCell
			));

			if (nextCell === null) {
				logError(
					'getNextCell',
					'nextCell null (safe), maxSafetyCells: ' +
					@maxSafetyCells + ', enemyCell: ' + @enemyCell
				);
				nextCell = @startCell;
			}

			if (MARK_SAFE_CELLS_TO_MOVE_ON && shouldMark) {
				mark(@maxSafetyCells, getColor(255, 128, 210));
			}
		} else {
			var refCells = maxSafetyLevel >= MIN_ACCEPTABLE_SAFE_LEVEL ?
				@maxSafetyCells :
				@reachableCells;

			nextCell = @(getFarthestCellFrom(
				@refCells, @enemyCell
			));

			if (nextCell === null) {
				logError(
					'getNextCell',
					'nextCell null (not safe), maxSafetyCells: ' +
					@maxSafetyCells + ', enemyCell: ' + @enemyCell
				);
				nextCell = @startCell;
			}
		}

		var summoner = getSummoner();

		if (isSummon()) {
			var summonerCell = @(getCell(@(getSummoner())));

			if (summonerCell !== null) {
				var distance = @(getDistance(@summonerCell, @startCell));
				var nextDistance = @(getDistance(@summonerCell, @nextCell));

				if (distance >= 2 && nextDistance <= 2) {
					logDetail('do not move not to block summoner');
					nextCell = @startCell;
				} else if (distance < 2 && safeCount(@reachableCells) > 0) {
					logDetail('move not to block summoner');
					nextCell = @(getFarthestCellFrom(
						@reachableCells, @summonerCell
					));

					if (nextCell === null) {
						logError(
							'getNextCell',
							'nextCell null (summon), reachableCells: ' +
							@reachableCells + ', summonerCell: ' + @summonerCell
						);
						nextCell = @startCell;
					}
				}
			} else {
				logWarning('getNextCell', 'summuner cell null');
			}
		}

		putInStore(@cacheKey, @nextCell);
		debugOperations();
	}

	logDetail('nextCell: ' + @nextCell);
	return @nextCell;
}

function getTargetEnemy(@weapon, @enemy) {
	var currentCell = getCell();
	var targetEnemy = null;

	if (canUseWeapon(weapon, enemy)) {
		targetEnemy = enemy;
	} else {
		// There may be another enemy between me and my target

		for (var aliveEnemy in @(getAliveEnemies())) {
			var aliveEnemyCell = getCell(aliveEnemy);
			var enemyCell = getCell(enemy);

			if (
				aliveEnemy !== enemy &&
				canUseWeapon(weapon, aliveEnemy) &&
				isOnSameLine(currentCell, aliveEnemyCell) &&
				isOnSameLine(currentCell, enemyCell) &&
				isOnSameLine(enemyCell, aliveEnemyCell) &&
				getDistance(enemyCell, aliveEnemyCell) < getDistance(enemyCell, currentCell)
			) {
				targetEnemy = aliveEnemy;
				break;
			}
		}
	}

	logInfo(
		'getTargetEnemy(): ' + getLeekName(targetEnemy) +
		(targetEnemy !== null ? (' on cell ' + getCell(targetEnemy)) : '')
	);

	return targetEnemy;
}

function initTurn() {
	clearStore();

	if (getTurn() === 1) {
		logInfo('Computes enemies ref arms');

		arrayIter(@(getAliveEnemies()), function(@enemy) {
			getEnemyRefArms(@enemy);
		});
	}

	if (LOG_MONITOR_DATA) {
		logMonitorData();
	}
}

function move(@enemy) {
	logInfo('move(' + getLeekName(enemy) + ')');
	increaseLogIndent();

	if (getMP() === 0) {
		return;
	}

	var debugOperations = monitorOperations('move', true);
	var nextCell = getNextCell(enemy, getCell(), getMP(), true);

	if (nextCell === null) {
		logError('move', 'unable to find the next cell, do not move');
		decreaseLogIndent();
		return;
	}

	var enemyPathLength = getEnemyPathLengthToFireOnCell(
		nextCell, enemy, !isSummon()
	);

	decreaseLogIndent();
	logDetail(
		'to ' + nextCell +
		' (safety: ' + getCellSafetyLevel(nextCell) +
		', path length: ' + enemyPathLength + ')'
	);

	if (nextCell !== getCell()) {
		moveTowardCell(nextCell);
	}
	debugOperations();
}

function setCellToFire(@arm, @target, @cellToFire) {
	putInStore('tmp-fire-cell-' + arm + '-' + target, @cellToFire);
}

function shouldFire(@target, @arm, @cellToFire, @pathlength) {
	logInfo('shouldFire()');

	var debugOperations = monitorOperations('shouldFire', true);
	var should = false;
	var willBadlyImpactAlly = false;

	if (!isAlly(target) && isAreaArm(@arm)) {
		var cellToFireOn = getCellToAreaFireOn(@arm, @target);

		if (cellToFireOn !== null) {
			var impactedLeeks = isChip(@arm) ?
				@(getChipTargets(@arm, @cellToFireOn)) :
				@(getWeaponTargets(@arm, @cellToFireOn));

			// getAliveAllies returns also me
			for (var ally in getAliveAllies()) {
				if (inArray(impactedLeeks, ally)) {
					logDetail('will badly impact ' + getLeekName(ally));
					willBadlyImpactAlly = true;
					break;
				}
			}
		} else {
			logError(
				'shouldFire',
				'unable to find cell to area fire with ' +
				getArmName(@arm) + ' on ' + getLeekName(@target)
			);
		}
	}

	if (!willBadlyImpactAlly) {
		if (pathlength === 0) {
			should = true;
			logDetail('can fire without moving');
		} else if (
			!isSummon(target) &&
			getLife() > (getTotalLife() * 2 / 3) &&
			getLife(target) < (getTotalLife(target) * 1 / 3)
		) {
			should = true;
			logDetail('finish him');
		} else {
			increaseLogIndent();

			// We do not want to take summons into account
			var initialEnemy = getNearestLeekEnemy();
			var safetyLevel = 'unknown';
			var nextCellAfterFire = getNextCell(
				initialEnemy, cellToFire, getMP() - pathlength, false
			);
			var safetyLevelAfter = 'unknown';
			var pathLengthAfter = 'unknown';
			var isSafe = false;
			var isSafeForPoison = false;
			var isSafeAfterInactivity = false;
			var isNotWorst = false;

			if (nextCellAfterFire !== null) {
				safetyLevelAfter = getCellSafetyLevel(nextCellAfterFire);
				pathLengthAfter = getEnemyPathLengthToFireOnCell(
					nextCellAfterFire, initialEnemy, false
				);
				isSafe = safetyLevelAfter >= SAFE_LEVEL;
				isSafeForPoison = target === initialEnemy &&
					(arm === CHIP_TOXIN || arm === CHIP_VENOM) &&
					!isPoisoned(initialEnemy) &&
					safetyLevelAfter >= 60;
				isSafeAfterInactivity = turnsWithoutFire >= 1 &&
					safetyLevelAfter >= 70;

				if (!isSafe && !isSafeForPoison && !isSafeAfterInactivity) {
					var nextCell = getNextCell(
						initialEnemy, getCell(), getMP(), false
					);

					if (nextCell !== null) {
						isNotWorst = safetyLevelAfter >= getCellSafetyLevel(nextCell);
					} else {
						logError('shouldFire', 'nextCell null');
					}
				}
			} else {
				logError('shouldFire', 'nextCellAfterFire null');
			}

			decreaseLogIndent();
			logDetail(
				(isSafe ? '' : 'not ') + 'safe (' + safetyLevelAfter + ', ' + pathLengthAfter + '), ' +
				(isSafeForPoison ? '' : 'not ') + 'safe for poison, ' +
				(isSafeAfterInactivity ? '' : 'not ') + 'safe after inactivity, ' +
				(isNotWorst ? 'not ' : '') + 'worst'
			);

			should = isSafe || isSafeForPoison || isSafeAfterInactivity || isNotWorst;
		}
	}

	debugOperations();
	return should;
}

function updateTurnsWithoutFire(@hasFired) {
	turnsWithoutFire = hasFired ? 0 : turnsWithoutFire + 1;
}
